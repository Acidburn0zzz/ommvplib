apply plugin: 'com.android.library'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def myVersionCode = versionMajor * 10000 + versionMinor * 100 + versionPatch
def myVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode myVersionCode
        versionName myVersionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
configurations {
    javadocDeps
}
buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:${supportVersion}"
    javadocDeps "com.android.support:support-annotations:${supportVersion}"
    testCompile 'junit:junit:4.12'
}

def credentialsFile = new File(System.properties['user.home'], ".bintray/bintray.properties")
ext {
    if (credentialsFile.canRead()) {
        def Properties props = new Properties()
        props.load(new FileInputStream(credentialsFile))
        bintrayUser = props['bintray.user']
        bintrayApiKey = props['bintray.apikey']
    }

    bintrayRepo = 'maven'
    bintrayName = 'ommvplib'

    publishedGroupId = 'eu.darken.ommvplib'
    libraryName = 'One More MVP Lib'
    artifact = 'library'

    libraryDescription = 'One More MVP Lib'

    siteUrl = 'https://github.com/d4rken/ommvplib'
    gitUrl = 'https://github.com/d4rken/ommvplib.git'

    libraryVersion = myVersionName

    developerId = 'darken'
    developerName = 'Matthias Urhahn'
    developerEmail = 'darken@darken.eu'

    licenseName = 'MIT'
    licenseUrl = 'https://github.com/d4rken/ommvplib/blob/master/LICENSE'
    allLicenses = ["MIT"]
}
if (credentialsFile.canRead()) {
    apply from: '../publish-to-bintray.gradle'
}